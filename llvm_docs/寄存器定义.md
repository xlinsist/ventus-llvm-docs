# 寄存器定义

在LLVM的后端代码生成中，存在大量重复性的信息管理，比如指令定义、寄存器定义。为了减少这些工作量，LLVM提供了TableGen工具供新架构的适配者使用。TableGen可以接受后缀为.td的文件，里面包含了对于特定架构的信息记录；使用TableGen可以管理并生成此架构下的各种后端文件。

乘影GPGPU的寄存器定义在 ventus/llvm-project/llvm/lib/Target/RISCV/VentusRegisterInfo.td 文件中，下面会结合架构文档手册对其比较重要的内容进行说明。

## 架构寄存器
### 寄存器的公共定义

所有寄存器的基类为RVRegisterClass，定义如下：

```
class RVRegisterClass <string n, list<ValueType> rTypes, int Align, dag rList>
  : RegisterClass <n, rTypes, Align, rList> {
  // vALU and sALU registers
  field bit IsVGPR = 0;
  field bit IsSGPR = 0;
  field bit IsFGPR = 0;

  let TSFlags{0} = IsVGPR;
  let TSFlags{1} = IsSGPR;
  let TSFlags{2} = IsFGPR;
}
```

注意其中TSFlag表示了寄存器的种类，这里的寄存器有三大类：vGPR、sGPR、fGPR。以RVRegisterClass为基础，定义了众多寄存器子类，比如GPR、VGPR、RISCVReg16等。

单个指令无法编码全部256个寄存器的场合，对于超过31号的寄存器调用需要使用regext转化成两条指令：

```
regext vd_base0, vs0_base0, vs1_base1 //这条指令告诉硬件使用 v[0-31] 作为 vd 和 vs1，使用 v[32-63] 作为 vs0。
vALUop vd, vs0, vs1                   //这条指令执行实际的向量 ALU 操作。
```

 关于regext相关的指令定义可参考4.2.2 寄存器扩展指令：

>REGEXT和REGEXTI指令用于扩展它之后一条指令的寄存器编码及立即数编码。在REGEXT 指令中，12位的立即数被分拆为四段3位数，分别拼接在下一条指令中寄存器rs3/vs3、rs2/vs2、rs1/vs1、rd/vd编码的高位上。REGEXTI指令则面向使用了5位立即数的指令，前缀包含6位的立即数高位， 以及rs2/vs2、rd/vd编码的高位。11立即数由前缀指令中的立即数高位与5位立即数直接拼接得到。  
>
>此外，在不使用扩展的情况下，向量浮点运算的vs3就是vd的[11:7]段，但进行向量扩展时，vs3 和vd的高3位分离存储。

### sGPR

#### 定义

在td文件中，sGPR的名字为X0到X63共64个，类别是GPR，定义如下：

```
// The order of registers represents the preferred allocation sequence.
// All sGPRs are callee saved registers.
def GPR : RVRegisterClass<"RISCV", [XLenVT], 32, (add
    (sequence "X%u", 5, 63),
    (sequence "X%u", 0, 4)
  )> {
  let RegInfos = XLenRI;
  let IsSGPR = 1;
}
```

所有的GPR都是调用者保存（Callee saved），定义顺序是建议的分配顺序；x0至x4因为被分配了特殊功能排在最后，根据指令集架构文档手册：

- x0：0寄存器；
- x1/ra：返回PC寄存器；
- x2/sp：栈指针 / local mem基址；
- x4/tp：private mem 基址。

部分sGPR会获得新的类别。

#### JALR指令推荐的目标寄存器

```
// Don't use X1 or X5 for JALR since that is a hint to pop the return address
// stack on some microarchitectures. Also remove the reserved registers X0, X2,
// X3, and X4 as it reduces the number of register classes that get synthesized
// by tablegen.
def GPRJALR : RVRegisterClass<"RISCV", [XLenVT], 32, (sub GPR, (sequence "X%u", 0, 5))> {
  let RegInfos = XLenRI;
  let IsSGPR = 1;
}
```

GPRJALR用于JALR的寄存器定义，其中移除了X0到X5的寄存器。X1和X5在某些微架构中会作为接受返回地址的目标寄存器所以弃用，并且为了减少tablegen生成的类的数量也移除了X0、X2、X3、X4。

#### 间接尾调用使用的寄存器

```
// For indirect tail calls, we can't use callee-saved registers, as they are
// restored to the saved value before the tail call, which would clobber a call
// address. We shouldn't use x5 since that is a hint for to pop the return
// address stack on some microarchitectures.
def GPRTC : RVRegisterClass<"RISCV", [XLenVT], 32, (add    
        (sequence "X%u", 6, 7),    
        (sequence "X%u", 10, 17),    
        (sequence "X%u", 28, 31)  )> {  
let RegInfos = XLenRI;  let IsSGPR = 1;}
```

GPRTC用于间接尾调用，包括X6、X7、X10到X17、X28到X31。这些寄存器不能使用被保存者调用寄存器；并且X5在某些微架构之中会用作返回地址所以也不能选择。

#### 栈指针/private mem基址寄存器

```
// TP is used as per-thread stack pointer on Ventus GPGPU.
def TP : RVRegisterClass<"RISCV", [XLenVT], 32, (add X4)> {
  let RegInfos = XLenRI;
  let IsSGPR = 1;
}
```

即每个线程的栈指针：X4或者叫TP。

#### SGPR的别名

参数传递中提到

>2.5.参数传递ABI
>
>对于kernel函数，a0是参数列表的基址指针，第一个clSetKernelArg设置的显存起始地址存入a0 register，kernel 默认从该位置开始加载参数。 对于非kernel函数，使用v0-v31和stackpointer传递参数，v0-v31作为返回值。

以下定义给出了这些SGPR的别名，可以发现x10就是a0，包括sp、tp、ra的定义也在其中：

```
let RegAltNameIndices = [ABIRegAltName] in {
  let isConstant = true in
  def X0  : RISCVReg<0, "x0", ["zero"]>, DwarfRegNum<[0]>;
  let CostPerUse = [0, 1] in {
  def X1  : RISCVReg<1, "x1", ["ra"]>, DwarfRegNum<[1]>;
  def X2  : RISCVReg<2, "x2", ["sp"]>, DwarfRegNum<[2]>;
  def X3  : RISCVReg<3, "x3", ["gp"]>, DwarfRegNum<[3]>;
  def X4  : RISCVReg<4, "x4", ["tp"]>, DwarfRegNum<[4]>;
  ...
  }
  ...
  def X10 : RISCVReg<10,"x10", ["a0"]>, DwarfRegNum<[10]>;
  ...
}
```



### vGPR

#### 定义

vGPR的名字为v0到v255，定义如下：

```
foreach Index = 0...255 in {
  defm V#Index :
    VentusRegLoHi16 <"v"#Index, Index>,
    DwarfRegNum<[!add(Index, 2560), !add(Index, 1536)]>;
}
def Reg16Types : RegisterTypes<[i16, f16]>;
def Reg32Types : RegisterTypes<[i32, f32, v2i16, v2f16]>;

// VGPR 32-bit registers class
def VGPR : RVRegisterClass<"RISCV",
                            !listconcat(Reg32Types.types, Reg16Types.types),
                            32,
                            (add (sequence "V%u", 0, 255))> {
  let Size = 32;
  let IsVGPR = 1;
}
```

vGPR的类别是VGPR，名字是V0到V255，注意到VGPR的类型同时包括了32位与16位，这是为了兼容3.1寄存器设置提到的这个特性

>OpenCL 中定义了一些向量类型，这些向量类型需要使用分组寄存器的形式表达，即 float16 在寄存器堆中以列存储，占用16个vGPR的各32位。这部分工作由编译器进行展开。

#### vGPR寄存器组的定义

VentusRegisterTuples用于定义寄存器组，这会在后面用于不同宽度的vGPR组。

```
class VentusRegisterTuples<list<SubRegIndex> Indices, RegisterClass RC,
                           int last_reg, int stride, int size, string prefix> :
  RegisterTuples<Indices,
                 RegSeqDags<RC, last_reg, stride, size>.ret,
                 RegSeqNames<last_reg, stride, size, prefix>.ret>;
```

#### 不同宽度的vGPR

使用VentusRegisterTuples定义，目前支持这些宽度：

```
// VGPR 64-bit registers
def VGPR_64 : VentusRegisterTuples<getSubRegs<2>.ret, VGPR, 255, 1, 2, "v">;

// VGPR 96-bit registers
def VGPR_96 : VentusRegisterTuples<getSubRegs<3>.ret, VGPR, 255, 1, 3, "v">;

// VGPR 128-bit registers
def VGPR_128 : VentusRegisterTuples<getSubRegs<4>.ret, VGPR, 255, 1, 4, "v">;

// VGPR 256-bit registers
def VGPR_256 : VentusRegisterTuples<getSubRegs<8>.ret, VGPR, 255, 1, 8, "v">;

// VGPR 512-bit registers
def VGPR_512 : VentusRegisterTuples<getSubRegs<16>.ret, VGPR, 255, 1, 16, "v">;

// VGPR 64 ~ 512 registers
def VReg_64  : RVRegisterClass<"RISCV", [i64, f64, v2i32, v2f32, v4f16, v4i16], 64, (add VGPR_64)>;
def VReg_96  : RVRegisterClass<"RISCV", [v3i32, v3f32], 64, (add VGPR_96)>;
def VReg_128 : RVRegisterClass<"RISCV", [v4i32, v4f32, v8i16, v8f16], 64, (add VGPR_128)>;
def VReg_256 : RVRegisterClass<"RISCV", [v8i32, v8f32, v16i16, v16f16], 64, (add VGPR_256)>;
def VReg_512 : RVRegisterClass<"RISCV", [v16i32, v16f32], 64, (add VGPR_512)>;

```

注意到里面有“v4f16, v4i16”这些定义了指定支持的数据类型，由于VGPR可以用作函数传参，实际上这里的定义就代表了乘影支持的传参数据类型。

#### 将VGPR当作16位访问

VentusRegLoHi16从32位的VGPR中抽象出了16位的访问，提供了这部分实现：

```
multiclass VentusRegLoHi16 <string n, bits<8> regIdx> {
  // There is no special encoding for 16 bit subregs, these are not real
  // registers but rather operands for instructions preserving other 16 bits
  // of the result or reading just 16 bits of a 32 bit VGPR.
  // It is encoded as a corresponding 32 bit register.
  // Non-VGPR register classes use it as we need to have matching subregisters
  // to move instructions and data between ALUs.
  def _LO16 : RISCVReg<regIdx, n#".l", [n#".l"]> {
    //let HWEncoding{8} = HWEncodingHigh;
  }
  def _HI16 : RISCVReg<regIdx, n#".h", [n#".h"]> {
    //let isArtificial = ArtificialHigh;
    //let HWEncoding{8} = HWEncodingHigh;
  }
  def "" : RegisterWithSubRegs<n, [!cast<Register>(NAME#"_LO16"),
                                   !cast<Register>(NAME#"_HI16")]> {
    let Namespace = "RISCV";
    let SubRegIndices = [lo16, hi16];
    let CoveredBySubRegs = 0; // TODO: Subreg disabled for now.
    let HWEncoding{7-0} = regIdx;
    let AltNames = [n];
    //let HWEncoding{8} = HWEncodingHigh;
  }
}
```



